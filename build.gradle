plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id 'io.github.goooler.shadow' version '8.1.7'
}

group = 'dev.furq'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name 'spigotmc-repo'
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {url = 'https://repo.jeff-media.com/public/'}
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'com.jeff-media:MorePersistentDataTypes:2.4.0'
    implementation 'com.jeff-media:custom-block-data:2.2.2'
    implementation 'com.jeff_media:SpigotUpdateChecker:3.0.3'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 17) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
kotlin {
    jvmToolchain(17)
}

shadowJar {
    relocate 'com.jeff_media.customblockdata', 'dev.furq.lib.customblockdata'
    relocate 'com.jeff_media.morepersistentdatatypes', 'dev.furq.lib.morepersistentdatatypes'
    relocate 'com.jeff_media.updatechecker', 'dev.furq.lib.updatechecker'
    archiveClassifier.set('')
    mergeServiceFiles()
}

build.dependsOn shadowJar